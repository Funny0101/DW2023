# 图数据库

### 节点建立

- (:Actor)

- (:Director)

- (:Movie)
  - 新添加一个字段：review_num，表示这部电影总的评论数量

  ```cypher
  :use system // 切换到system数据库
  show database // 显示当前已有数据库
  CREATE DATABASE DW // 新建数据库，我是就在neo4j上建的，新建数据库没搞定
  //删除所有节点
  MATCH (n)
  DETACH DELETE n;
  
  ```
  
  ```cypher
  // 在Actor节点上创建约束
  CREATE CONSTRAINT FOR (a:Actor) REQUIRE a.actor_id IS UNIQUE;
  
  // 导入Actor数据
  LOAD CSV WITH HEADERS FROM 'file:///actor.csv' AS row
  CREATE (:Actor {actor_id: toInteger(row.actor_id), actor_name: row.actor_name});
  ```
  
  ```cypher
  // 在Director节点上创建约束
  CREATE CONSTRAINT FOR (d:Director) REQUIRE d.director_id IS UNIQUE;
  
  // 导入Director数据
  LOAD CSV WITH HEADERS FROM 'file:///director.csv' AS row
  CREATE (:Director {director_id: toInteger(row.director_id), director_name: row.director_name});
  ```
  
  ```cypher
  // 在Movie节点上创建约束
  CREATE CONSTRAINT FOR (m:Movie) REQUIRE m.movie_id IS UNIQUE;
  
  // 导入Movie数据
  LOAD CSV WITH HEADERS FROM 'file:///movie.csv' AS row
  CREATE (:Movie {movie_id: row.ASIN, movie_title: row.Title, movie_genre: row.Genres, movie_review_num: row.review_num});
  ```

### 关系建立

- (:Actor)-[:ACTED]->(:Movie)：演员参演了哪些电影

- (:Director)-[:DIRECT]->(:Movie)：导演执导了哪些电影

  ```cypher
  // 导入演员参演关系
  LOAD CSV WITH HEADERS FROM 'file:///act.csv' AS row
  MATCH (a:Actor {actor_id: toInteger(row.actor_id)}), (m:Movie {movie_id: row.movie_id})
  CREATE (a)-[:ACTED {is_main_actor: row.is_main_actor}]->(m);
  ```

  ```cypher
  // 导入导演执导关系
  LOAD CSV WITH HEADERS FROM 'file:///direct.csv' AS row
  MATCH (a:Director {director_id: toInteger(row.director_id)}), (m:Movie {movie_id: row.movie_id})
  CREATE (a)-[:DIRECT]->(m);
  ```

### 合作关系查询

#### 经常合作的演员

```cypher
MATCH (a1:Actor)-[:ACTED]->(:Movie)<-[:ACTED]-(a2:Actor)
WHERE ID(a1) < ID(a2)
RETURN a1.actor_name, a2.actor_name, COUNT(*) AS collaboration_count
ORDER BY collaboration_count DESC
```

#### 经常合作的导演和演员

```cypher
MATCH (d:Director)-[:DIRECT]->(:Movie)<-[:ACTED]-(a:Actor)
RETURN a.actor_name, d.director_name, COUNT(*) AS collaboration_count
ORDER BY collaboration_count DESC
```

#### 某类型下评论最多的演员组合

```cypher
MATCH (a1:Actor)-[:ACTED]->(m:Movie {movie_genre: 'Action & Adventure'})<-[:ACTED]-(a2:Actor)
WHERE ID(a1) < ID(a2) AND m.movie_review_num IS NOT NULL
RETURN a1.actor_name, a2.actor_name, SUM(COALESCE(TOINTEGER(m.movie_review_num), 0)) AS total_reviews
ORDER BY total_reviews DESC
LIMIT 1

// 三人
MATCH (a1:Actor)-[:ACTED]->(m1:Movie {movie_genre: 'Drama'})<-[:ACTED]-(a2:Actor)-[:ACTED]->(m2:Movie {movie_genre: 'Drama'})<-[:ACTED]-(a3:Actor)
WHERE ID(a1) < ID(a2) AND ID(a2) < ID(a3) AND m1.movie_review_num IS NOT NULL AND m2.movie_review_num IS NOT NULL
RETURN a1.actor_name, a2.actor_name, a3.actor_name, SUM(COALESCE(TOINTEGER(m1.movie_review_num), 0) + COALESCE(TOINTEGER(m2.movie_review_num), 0)) AS total_reviews
ORDER BY total_reviews DESC
LIMIT 1
```

